plugins {
    id "io.spring.dependency-management" version "1.0.3.RELEASE"
    id "org.springframework.boot" version "1.5.6.RELEASE"
}

group = "${group}"
version = "${version}"

bootRepackage.enabled = false

allprojects {
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-starter-parent:Dalston.SR2"
        }

        dependencies {
            dependency 'org.projectlombok:lombok:1.16.18'
            dependency 'org.codehaus.groovy:groovy-all:2.4.9'
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    // TODO: Execution failed for task ':XXX-service:findMainClass'.
    // > Could not set unknown property 'mainClassName' for project ':f1-service'
    apply plugin: 'application'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    jar {
        archiveName = project.name + ".jar"
    }

    dependencies {
        compile 'org.codehaus.groovy:groovy-all'

        compile 'org.springframework.cloud:spring-cloud-starter-zipkin'
        compile 'org.springframework.cloud:spring-cloud-sleuth-zipkin'

        compile 'org.springframework.cloud:spring-cloud-starter-eureka'
//    compile 'org.springframework.cloud:spring-cloud-starter-consul-all'

        compileOnly 'org.projectlombok:lombok'

        testCompile 'org.springframework.boot:spring-boot-starter-test'
    }

    task dockerBuild {
        doLast {
            exec {
                commandLine 'docker', 'build', '--force-rm=true', "--tag='${project.name}'", '.'
            }
        }
    }

    dockerBuild.dependsOn build

    springBoot {
        executable = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}
